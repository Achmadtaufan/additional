-- FINAL: cek control chars 0..31 di semua kolom teks, tampil hanya kolom bermasalah
DECLARE @table SYSNAME = 'ztemptes';
DECLARE @col SYSNAME;
DECLARE @sql NVARCHAR(MAX);
DECLARE @n INT;

IF OBJECT_ID('tempdb..#badChars') IS NOT NULL DROP TABLE #badChars;
CREATE TABLE #badChars (
    ColumnName SYSNAME,
    BadCharCount BIGINT
);

DECLARE cur CURSOR FAST_FORWARD FOR
SELECT name
FROM sys.columns
WHERE object_id = OBJECT_ID(@table)
  AND system_type_id IN (167,175,231,239)  -- varchar, char, nvarchar, nchar
ORDER BY column_id;

OPEN cur;
FETCH NEXT FROM cur INTO @col;

WHILE @@FETCH_STATUS = 0
BEGIN
    -- build condition: CHARINDEX(NCHAR(0), [col])>0 OR CHARINDEX(NCHAR(1), [col])>0 ...
    SET @sql = N'';
    SET @n = 0;
    WHILE @n <= 31
    BEGIN
        IF LEN(@sql) = 0
            SET @sql = N'CHARINDEX(NCHAR(' + CAST(@n AS NVARCHAR(3)) + N'), ' + QUOTENAME(@col) + N') > 0';
        ELSE
            SET @sql = @sql + N' OR CHARINDEX(NCHAR(' + CAST(@n AS NVARCHAR(3)) + N'), ' + QUOTENAME(@col) + N') > 0';
        SET @n = @n + 1;
    END

    -- insert count into temp table
    SET @sql = N'INSERT INTO #badChars (ColumnName, BadCharCount)
                 SELECT ' + QUOTENAME(@col,'''') + N' AS ColumnName, COUNT(*) AS BadCharCount
                 FROM ' + QUOTENAME(@table) + N'
                 WHERE ' + @sql + N';';

    EXEC sp_executesql @sql;

    FETCH NEXT FROM cur INTO @col;
END

CLOSE cur;
DEALLOCATE cur;

-- tampilkan hanya kolom yang bermasalah
SELECT ColumnName, BadCharCount
FROM #badChars
WHERE BadCharCount > 0
ORDER BY BadCharCount DESC;

-- bersihin
DROP TABLE #badChars;